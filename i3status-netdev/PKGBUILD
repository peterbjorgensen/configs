pkgname=i3status-netdev-git
pkgver=20200212
pkgrel=1
pkgdesc="Generates status bar to use with dzen2 or wmii. Patched to use /proc/net/dev on linux to show bandwidth use"
arch=('i686' 'x86_64')
url="http://i3.zekjur.net/i3status/"
license=('BSD')
depends=('confuse' 'alsa-lib' 'yajl' 'libpulse' 'libnl')
makedepends=('git' 'pkgconfig' 'asciidoc' 'xmlto')
options=('docs')
install=i3status.install
conflicts=('i3status')
provides=('i3status')
source=(git+https://github.com/peterbjorgensen/i3status-netdev)
sha1sums=('SKIP')

_gitname="i3status-netdev"

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --date=format:"%Y%m%d" --format="%ad"
}

build() {
  cd "$_gitname"

  autoreconf --force --install

  rm -rf build/
  mkdir -p build && cd build/

  ../configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-sanitizers

  # See https://lists.archlinux.org/pipermail/arch-dev-public/2013-April/024776.html
  make CFLAGS=""
}

package() {
  cd "$_gitname"
  cd build/

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
